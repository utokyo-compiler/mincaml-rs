use sourcemap::{Loc, Spanned};
use syntax::{*, ExprKind::*, LitKind::*, IntBinOpKind::*, RelationBinOpKind::*, FloatBinOpKind::*, UnOp::*};

use crate::lexer::{Error as LexError, Token};
use super::{Allocator, CtorTranspose, CtorContinuation};

grammar<'input, 'ctx>(alloc: Allocator<'ctx>);
extern {
    type Location = Loc;
    type Error = LexError<'input>;

    enum Token<'input> {
        "(" => Token::LPar,
        ")" => Token::RPar,
        Bool => Token::Bool(<bool>),
        Int => Token::Int(<i32>),
        Float => Token::Float(<f32>),
        Ident => Token::Ident(<&'input str>),
        "-" => Token::Hyphen,
        "+" => Token::Plus,
        "*" => Token::Ast,
        "/" => Token::Slash,
        "-." => Token::HyphenDot,
        "+." => Token::PlusDot,
        "*." => Token::AstDot,
        "/." => Token::SlashDot,
        "=" => Token::Equal,
        "<>" => Token::LessGreater,
        "<=" => Token::LessEqual,
        ">=" => Token::GreaterEqual,
        "<" => Token::Less,
        ">" => Token::Greater,
        "if" => Token::If,
        "then" => Token::Then,
        "else" => Token::Else,
        "let" => Token::Let,
        "in" => Token::In,
        "rec" => Token::Rec,
        "," => Token::Comma,
        "Array.make" => Token::ArrayMake,
        "." => Token::Dot,
        "<-" => Token::LessHyphen,
        ";" => Token::Semi,
    }
}

SpannedIdent: Spanned<Ident<'ctx>> = {
    <l:@L> <x:Ident> <r:@R> => Spanned::new(alloc.ctx().intern_ident(x), (l, r)),
}

TermOp: BinOp = {
    "+" => BinOp::Int(Add),
    "-" => BinOp::Int(Sub),
    "+." => BinOp::Float(FAdd),
    "-." => BinOp::Float(FSub)
};

FactorOp: BinOp = {
    "*" => BinOp::Int(Mul),
    "/" => BinOp::Int(Div),
    "*." => BinOp::Float(FMul),
    "/." => BinOp::Float(FDiv)
};

RelationBinOp: BinOp = {
    "=" => BinOp::Relation(Eq),
    "<=" => BinOp::Relation(Le),
    ">=" => BinOp::Relation(Ge),
    "<>" => BinOp::Relation(Ne),
    "<" => BinOp::Relation(Lt),
    ">" => BinOp::Relation(Gt)
};

Literal: Expr<'ctx> = {
    <l:@L> "(" ")" <r:@R> => alloc.spanned(Const(Unit), (l, r)),
    <l:@L> <x:Bool> <r:@R> => alloc.spanned(Const(Bool(x)), (l, r)),
    <l:@L> <x:Int> <r:@R> => alloc.spanned(Const(Int(x)), (l, r)),
    <l:@L> <x:Float> <r:@R> => alloc.spanned(Const(Float(x.to_bits())), (l, r)),
}

SimpleExpr: Expr<'ctx> = {
    "(" <x:Expr> ")" => x,
    <l:@L> "(" <e:Elems> ")" <r:@R>
        => alloc.spanned(Tuple(e), (l, r)),
    Literal,
    <l:@L> <x:Ident> <r:@R> => alloc.spanned(Var(alloc.ctx().intern_ident(x)), (l, r)),
    <l:@L> <s:SimpleExpr> "." "(" <x:Expr> ")" <r:@R> => alloc.spanned(Get(s, x), (l, r)),
}

ExprPrec5: Expr<'ctx> = {
    #[precedence(level="0")]
    SimpleExpr,

    // (a b)
    #[precedence(level="1")]
    <l:@L> <e1:SimpleExpr> <e2:ActualArgs> <r:@R>
        => alloc.spanned(App(e1, e2), (l, r)),
    <l:@L> "Array.make" <e1:SimpleExpr> <e2:ExprPrec5> <r:@R>
        => alloc.spanned(ArrayMake(e1, e2), (l, r)),

    // -
    #[precedence(level="2")]
    <l:@L> "-" <e1:ExprPrec5> <r:@R>
        => alloc.spanned(Unary(Neg, e1), (l, r)),

    // -.
    <l:@L> "-." <e1:ExprPrec5> <r:@R>
        => alloc.spanned(Unary(FNeg, e1), (l, r)),

    // * *. /.
    #[precedence(level="3")]
    #[assoc(side="left")]
    <l:@L> <e1:ExprPrec5> <op:FactorOp> <e2:ExprPrec5> <r:@R>
        => alloc.spanned(Binary(op, e1, e2), (l, r)),

    // + - +. -.
    #[precedence(level="4")]
    #[assoc(side="left")]
    <l:@L> <e1:ExprPrec5> <op:TermOp> <e2:ExprPrec5> <r:@R>
        => alloc.spanned(Binary(op, e1, e2), (l, r)),

    // = <> < > <= >=
    #[precedence(level="5")]
    #[assoc(side="left")]
    <l:@L> <e1:ExprPrec5> <op:RelationBinOp> <e2:ExprPrec5> <r:@R>
        => alloc.spanned(Binary(op, e1, e2), (l, r)),
}

ArraySet: CtorContinuation<'ctx> = {
    <l:@L> <e1:SimpleExpr> "<-"
        => CtorContinuation::DelayedSet { e1, l },
}

// min-caml allows semicolon in then branch
AfterThen = Expr;

// Expression that starts with if
If: CtorContinuation<'ctx> = {
    // if
    <l:@L> "if" <e1:Expr> "then" <e2:AfterThen> "else"
        => CtorContinuation::DelayedIf { e1, e2, l },
}

NoSemi = ExprPrec5;

// Expression that can follow `else` or `<-`
AfterElseArrow: CtorTranspose<'ctx> = {
    // `if` inside of (`if` or `set`) or `if`. continue.
    <cont:If> <trans:AfterElseArrow> => trans.composed_left(cont),

    // `set` inside of (`if` or `set`) or `set`. continue.
    <cont:ArraySet> <trans:AfterElseArrow> => trans.composed_left(cont),

    // `if e1 then e2 else let ..` or `e1 <- let ..` or `let ..`
    //
    // Looking ahead the semicolon after the second expression (e:ExprPrec9) in `Let` should trigger the shift action to OptionalSemi rule within `ExprPrec9` in `Let`,
    // not the reduce action here.
    <e:Let> <r:@R> => CtorTranspose::new_eval(e, r),

    // `if e1 then e2 else e3; e4`
    //                       ^ here
    // In this case, the proper syntax tree is `(if e1 then e2 else e3); e4`.
    Semi,
}

// Expression that can have semicolon, which is a *singularity* of the OCaml syntax.
Semi: CtorTranspose<'ctx> = {
    <l:@L> <e1:NoSemi> <e2:OptionalSemi?> <r2:@R> => match e2 {
        Some((r, e2)) => CtorTranspose::new_delayed_then(e1, r, e2, l, r2),
        None => CtorTranspose::new_eval(e1, r2),
    },
}

#[inline]
OptionalSemi: (Loc, Expr<'ctx>) = {
    <r:@R> ";" <e2:ExprPrec9> => (r, e2),
}

// Expression that starts with let
Let: Expr<'ctx> = {
    // let
    <l:@L> "let" <binder:LetBinder> "in" <e:ExprPrec9> <r:@R>
        => alloc.spanned(Let(binder, e), (l, r)),
}

ExprPrec9: Expr<'ctx> = {
    AfterElseArrow => alloc.apply(<>),
}

pub Expr = ExprPrec9;

LetBinder: LetBinder<'ctx> = {
    <x:SpannedIdent> "=" <val:Expr>
        => LetBinder::let_var(x, val),

    "rec" <x:SpannedIdent> <arg:FormalArgs> "=" <val:Expr>
        => LetBinder::let_rec(x, arg, val),

    <p:Pat> "=" <val:Expr>
        => LetBinder::let_tuple(p, val),
}

FormalArgs: Vec<Spanned<Ident<'ctx>>> = {
    <mut a:FormalArgs> <ident:SpannedIdent> => {
        a.push(ident);
        a
    },
    SpannedIdent => vec![<>],
}

ActualArgs: Vec<Expr<'ctx>> = {
    <mut a:ActualArgs> <s:SimpleExpr> => {
        a.push(s);
        a
    },
    SimpleExpr => vec![<>],
}

Elems: Vec<Expr<'ctx>> = {
    <mut a:Elems> "," <i:Expr> => {
        a.push(i);
        a
    },
    <i1:Expr> "," <i2:Expr> => vec![i1, i2],
}

PatRaw: Vec<Spanned<Ident<'ctx>>> = {
    <mut a:PatRaw> "," <ident:SpannedIdent> => {
        a.push(ident);
        a
    },
    <ident1:SpannedIdent> "," <ident2:SpannedIdent> => vec![ident1, ident2],
}

Pat: Vec<Spanned<Ident<'ctx>>> = {
    PatRaw,
    "(" <p:PatRaw> ")" => p,
}
