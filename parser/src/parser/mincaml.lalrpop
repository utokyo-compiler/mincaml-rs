use sourcemap::{Loc, Spanned};
use syntax::{*, ExprKind::*, LitKind::*, IntBinOpKind::*, BooleanBinOpKind::*, FloatBinOpKind::*, UnOp::*};

use crate::lexer::{Error as LexError, Token};
use super::Allocator;

grammar<'input, 'ctx>(alloc: Allocator<'ctx>);
extern {
    type Location = Loc;
    type Error = LexError<'input>;

    enum Token<'input> {
        "(" => Token::LPar,
        ")" => Token::RPar,
        "not" => Token::Not,
        Bool => Token::Bool(<bool>),
        Int => Token::Int(<i32>),
        Float => Token::Float(<f32>),
        Ident => Token::Ident(<&'input str>),
        "-" => Token::Hyphen,
        "+" => Token::Plus,
        "*" => Token::Ast,
        "/" => Token::Slash,
        "-." => Token::HyphenDot,
        "+." => Token::PlusDot,
        "*." => Token::AstDot,
        "/." => Token::SlashDot,
        "=" => Token::Equal,
        "<>" => Token::LessGreater,
        "<=" => Token::LessEqual,
        ">=" => Token::GreaterEqual,
        "<" => Token::Less,
        ">" => Token::Greater,
        "if" => Token::If,
        "then" => Token::Then,
        "else" => Token::Else,
        "let" => Token::Let,
        "in" => Token::In,
        "rec" => Token::Rec,
        "," => Token::Comma,
        "Array.make" => Token::ArrayMake,
        "." => Token::Dot,
        "<-" => Token::LessHyphen,
        ";" => Token::Semi,
    }
}

SpannedIdent: Spanned<Ident<'ctx>> = {
    <l:@L> <x:Ident> <r:@R> => Spanned::new(alloc.ctx().intern_ident(x), (l, r)),
}

TermOp: BinOp = {
    "+" => BinOp::Int(Add),
    "-" => BinOp::Int(Sub),
    "+." => BinOp::Float(FAdd),
    "-." => BinOp::Float(FSub)
};

FactorOp: BinOp = {
    "*" => BinOp::Int(Mul),
    "/" => BinOp::Int(Div),
    "*." => BinOp::Float(FMul),
    "/." => BinOp::Float(FDiv)
};

BooleanBinOp: BinOp = {
    "=" => BinOp::Boolean(Eq),
    "<=" => BinOp::Boolean(Le),
    ">=" => BinOp::Boolean(Ge),
    "<>" => BinOp::Boolean(Ne),
    "<" => BinOp::Boolean(Lt),
    ">" => BinOp::Boolean(Gt)
};

Literal: Expr<'ctx> = {
    <l:@L> "(" ")" <r:@R> => alloc.spanned(Const(Unit), (l, r)),
    <l:@L> <x:Bool> <r:@R> => alloc.spanned(Const(Bool(x)), (l, r)),
    <l:@L> <x:Int> <r:@R> => alloc.spanned(Const(Int(x)), (l, r)),
    <l:@L> <x:Float> <r:@R> => alloc.spanned(Const(Float(x.to_bits())), (l, r)),
}

SimpleExpr: Expr<'ctx> = {
    "(" <x:Expr> ")" => x,
    <l:@L> "(" <e:Elems> ")" <r:@R>
        => alloc.spanned(Tuple(e), (l, r)),
    Literal,
    <l:@L> <x:Ident> <r:@R> => alloc.spanned(Var(alloc.ctx().intern_ident(x)), (l, r)),
    <l:@L> <s:SimpleExpr> "." "(" <x:Expr> ")" <r:@R> => alloc.spanned(Get(s, x), (l, r)),
}

pub Expr: Expr<'ctx> = {
    #[precedence(level="0")]
    SimpleExpr,

    // (a b)
    #[precedence(level="1")]
    <l:@L> <e1:SimpleExpr> <e2:ActualArgs> <r:@R>
        => alloc.spanned(App(e1, e2), (l, r)),
    <l:@L> "Array.make" <e1:SimpleExpr> <e2:Expr> <r:@R>
        => alloc.spanned(ArrayMake(e1, e2), (l, r)),
    <l:@L> "not" <e1:SimpleExpr> <r:@R>
        => alloc.spanned(Unary(Not, e1), (l, r)),

    // -
    #[precedence(level="2")]
    <l:@L> "-" <e1:Expr> <r:@R>
        => alloc.spanned(Unary(Neg, e1), (l, r)),

    // -.
    <l:@L> "-." <e1:Expr> <r:@R>
        => alloc.spanned(Unary(FNeg, e1), (l, r)),

    // * *. /.
    #[precedence(level="3")]
    #[assoc(side="left")]
    <l:@L> <e1:Expr> <op:FactorOp> <e2:Expr> <r:@R>
        => alloc.spanned(Binary(op, e1, e2), (l, r)),

    // + - +. -.
    #[precedence(level="4")]
    #[assoc(side="left")]
    <l:@L> <e1:Expr> <op:TermOp> <e2:Expr> <r:@R>
        => alloc.spanned(Binary(op, e1, e2), (l, r)),

    // = <> < > <= >=
    #[precedence(level="5")]
    #[assoc(side="left")]
    <l:@L> <e1:Expr> <op:BooleanBinOp> <e2:Expr> <r:@R>
        => alloc.spanned(Binary(op, e1, e2), (l, r)),

    // .() <-
    #[precedence(level="6")]
    <l:@L> <e1:SimpleExpr> "<-" <e2:Expr> <r:@R>
        => alloc.spanned(Set(e1, e2), (l, r)),

    // if
    #[precedence(level="7")]
    <l:@L> "if" <e1:Expr> "then" <e2:Expr> "else" <e3:Expr> <r:@R>
        => alloc.spanned(If(e1, e2, e3), (l, r)),

    // semicolon
    #[precedence(level="8")]
    #[assoc(side="right")]
    <l:@L> <e1:Expr> ";" <e2:Expr> <r:@R> => alloc.spanned(
        Then(e1, e2),
        (l, r)
    ),

    // let
    #[precedence(level="9")]
    <l:@L> "let" <binder:LetBinder> "in" <e:Expr> <r:@R>
        => alloc.spanned(Let(binder, e), (l, r)),
}

LetBinder: LetBinder<'ctx> = {
    <x:SpannedIdent> "=" <val:Expr>
        => LetBinder::let_var(x, val),

    "rec" <x:SpannedIdent> <arg:FormalArgs> "=" <val:Expr>
        => LetBinder::let_rec(x, arg, val),

    <p:Pat> "=" <val:Expr>
        => LetBinder::let_tuple(p, val),
}

FormalArgs: Vec<Spanned<Ident<'ctx>>> = {
    <mut a:FormalArgs> <ident:SpannedIdent> => {
        a.push(ident);
        a
    },
    SpannedIdent => vec![<>],
}

ActualArgs: Vec<Expr<'ctx>> = {
    <mut a:ActualArgs> <s:SimpleExpr> => {
        a.push(s);
        a
    },
    SimpleExpr => vec![<>],
}

Elems: Vec<Expr<'ctx>> = {
    <mut a:Elems> "," <i:Expr> => {
        a.push(i);
        a
    },
    <i1:Expr> "," <i2:Expr> => vec![i1, i2],
}

PatRaw: Vec<Spanned<Ident<'ctx>>> = {
    <mut a:PatRaw> "," <ident:SpannedIdent> => {
        a.push(ident);
        a
    },
    <ident1:SpannedIdent> "," <ident2:SpannedIdent> => vec![ident1, ident2],
}

Pat: Vec<Spanned<Ident<'ctx>>> = {
    PatRaw,
    "(" <p:PatRaw> ")" => p,
}
